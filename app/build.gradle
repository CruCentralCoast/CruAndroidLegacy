apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'jacoco'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    sourceSets {
        String sharedResources = 'src/shared/resources'
        String sharedJava = 'src/shared/java'
        test {
            resources.srcDir sharedResources
            java.srcDir sharedJava
        }
        androidTest {
            resources.srcDir sharedResources
            java.srcDir sharedJava
        }
    }

    defaultConfig {
        applicationId "org.androidcru.crucentralcoast"
        testInstrumentationRunner applicationId + ".JUnitJacocoTestRunner"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 13
        versionName "1.2.3"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "YOUTUBEAPIKEY", '"AIzaSyDOrCdWh_DX1DuoSvP4-os2tn_GIQDpVnw"'
        buildConfigField "String", "YOUTUBEBROWSERAPIKEY", '"AIzaSyAwJqxJqGojYiiM1Y81Vfy0bcuZIt6OQdg"'
        buildConfigField "String", "YOUTUBEBASEURL", '"https://www.googleapis.com"'
    }

    dexOptions {
        jumboMode true
        incremental false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testBuildType "staging"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CRU_SERVER", '"http://androidcru-capstone.jonamireh.com"'
        }
        debug {
            buildConfigField "String", "CRU_SERVER", '"http://androidcru-dev.jonamireh.com"'
        }
        staging {
            minifyEnabled true
            testCoverageEnabled = true
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        disable 'InvalidPackage'
        check 'NewApi'
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //UI Libraries
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:percent:23.4.0'
    compile 'com.android.support:customtabs:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:preference-v14:23.4.0'

    compile 'com.mobsandgeeks:android-saripaar:2.0.3'
    compile 'com.wdullaer:materialdatetimepicker:2.3.0'
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'com.jakewharton:butterknife:8.0.1'

    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    //GCM
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-identity:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'

    //Network and JSON
    debugCompile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    releaseCompile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    testCompile 'org.threeten:threetenbp:1.3.1'
    compile('org.aaronhe:threetenbp-gson-adapter:1.0.2') {
        transitive = false
    }
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.facebook.android:facebook-android-sdk:4.6.0'

    compile 'com.anupcowkur:reservoir:2.1'
    compile 'org.parceler:parceler-api:1.1.1'
    apt 'org.parceler:parceler:1.1.1'


    //Rx
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.4'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    compile 'com.artemzin.rxjava:proguard-rules:1.1.2.0'
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'
    //Logging
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'net.ypresto.timbertreeutils:timbertreeutils:1.0.0'
    compile 'com.github.orhanobut:logger:1.12'
    //Image Manipulation
    compile 'jp.wasabeef:picasso-transformations:2.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Set this dependency to build and run Espresso tests
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    // Espresso-web for WebView support
    androidTestCompile('com.android.support.test.espresso:espresso-web:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    // Espresso-idling-resource for synchronization with background jobs
    androidTestCompile('com.android.support.test.espresso:espresso-idling-resource:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    androidTestCompile 'junit:junit:4.12'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    //androidTestCompile 'org.powermock:powermock-module-junit4:1.6.5'
    //androidTestCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.skyscreamer:jsonassert:1.3.0'

    testCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}


apply plugin: 'com.google.gms.google-services'
